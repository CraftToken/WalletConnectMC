plugins {
    id 'java'
    alias libs.plugins.shadow
    id 'com.github.spotbugs' version '5.0.7' apply false
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'
    
    group = 'org.craftcoin.walletconnectmc'
    version = '0.1.0-SNAPSHOT'

    java.sourceCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        
        // Velocity
        maven {
            url = uri('https://repo.papermc.io/repository/maven-public/')
        }

        // Spigot
        maven {
            url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
        }

        // kotlin-walletconnect-lib, miniplaceholders
        maven {
            url = uri('https://jitpack.io')
        }
    }
    
    pmd {
        consoleOutput = true
        toolVersion = '6.46.0'
        ruleSetFiles = files(new File(rootProject.rootDir, 'config/pmd/pmd.xml'))
        ruleSets.clear()
    }

    spotbugs {
        ignoreFailures = false
        showStackTraces = true
        showProgress = true
        reportLevel = 'HIGH'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':velocity')
    implementation project(':paper')
    implementation project(':api')
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

shadowJar {
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }

    archiveClassifier.set('')
}

artifacts {
    archives shadowJar
}
